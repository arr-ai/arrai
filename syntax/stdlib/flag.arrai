\stdlib
    let (:re, :error, :eval, ...) = stdlib;
    let dashes = re.compile(`^--?`);
    let trimDashes = dashes.sub('');
    # regex from arrai.wbnf
    let isNumber = re.compile(`^\-?(?:\d+(?:\.\d*)?|\.\d+)(?:[Ee][-+]?\d+)?$`).match;
    let types = {'string', 'bool', 'number'};
    (
        help: \config
                let config = config orderby .@ >> \(@: flagName, @value: flagConfig)
                    cond flagConfig {
                        (:type, ?:usage:'', ?:alias:'', ?:default:'', ...): (:flagName, :usage, :alias, :type, :default),
                        type: (:flagName, :type, alias: '', usage: '', default: ''),
                    }
                ;
                $`
                    Options:
                        ${config >> $`
                            --${.flagName}${.alias && $`, -${.alias}`}${.type != 'bool' && ' ' ++ .type}${
                                .default && ' ' ++ $`(default: ${.default})`
                            }
                                    ${.usage}
                            `::\i}
                `,
        parser: \config
            let _ = cond config {{...}: '', _: error($`config must be a dictionary, not ${config}`)};
            let fn = \val \head
                let _ = cond head {(@: _, @value: _): '', _: error($`config must be a dictionary, not ${config}`)};
                let alias = cond head {
                    (@: name, @value: (:alias, ...)):
                        let dupl = val.alias(alias)?:false;
                        cond {
                            dupl: error($`alias '${alias}' aliases multiple flags: '${dupl}' and '${name}'`),
                            _: {alias: name},
                        }
                    ,
                    _: {},
                };

                # handle types config
                let flagConfigs = cond head.@value {
                    (:default, :type, ...): (:type, :default, hasDefault: true),
                    (:type, ...):           (:type, hasDefault: false),
                    type:                   (:type, hasDefault: false),
                    _:                      error($`flag config invalid: ${head}`),
                };

                let _ = cond {
                    flagConfigs.type !<: types: error($`type must be one of ${types orderby .}, not ${type}`)
                };

                # handle repeated config
                let flagConfigs = flagConfigs +> cond head.@value {
                    (:repeated, ...): (:repeated),
                    _: (),
                };

                let name = head.@;

                (alias: val.alias +> alias, flagConfigs: val.flagConfigs +> {name: flagConfigs})
            ;
            let config = //{./util}.reduce(
                fn, (alias: {}, flagConfigs: {}), config orderby .,
            );

            # TODO: help message
            let parse = \args
                let flagConfig = \flagName
                    cond {
                        dashes.match(flagName):
                            let name =
                                let trimmed = trimDashes(flagName);
                                let name = cond //seq.split('=', trimmed) {[name, _]: name, _: trimmed};
                                config.alias(name)?:name
                            ;

                            let (?:type:'', ?:repeated:false, ...) = config.flagConfigs(name)?:();

                            (flagName: name, :type, :repeated),
                        ,
                        _: (flagName: '', type: '', repeated: false),
                    }
                ;

                let rec parseFlag = \args \val
                    cond args {
                        [flag, ...tail]:
                            let (:flagName, :type, :repeated) = flagConfig(flag);
                            # adds flag value to the accumulated flags so far
                            let withFlagValue = \value
                                let value = cond {repeated: val.flags(flagName)?:[] ++ [value], _: value};
                                val +> (flags: val.flags +> {flagName: value})
                            ;

                            # if flag is of format --flag=value, add value to tail
                            let tail = cond //seq.split('=', flag) {[_, value]: [value] ++ tail, _: tail};

                            cond type {
                                'bool': parseFlag(tail, withFlagValue(true)),
                                'string':
                                    cond tail {
                                        [value, ...rest]:
                                            parseFlag(rest, withFlagValue(value))
                                        ,
                                        _: error($`string flag does not have a value: '${flagName}'`),
                                    },
                                'number': cond tail {
                                    [value, ...rest]: cond {
                                        isNumber(value): parseFlag(
                                            rest, withFlagValue(eval.value(value)),
                                        ),
                                        _: error($`value is not a number: '${value}'`),
                                    },
                                    _: error($`number flag does not have a value: '${flagName}'`),
                                },
                                _: parseFlag(tail, val +> (args: val.args ++ [flag])),
                            }
                        ,
                        _: val
                    }
                ;
                let parsed = parseFlag(args, (args: [], flags: {}));
                (
                    :parsed.args,
                    flags: parsed.flags |
                        # add the flags with defaults
                        let existingFlags = parsed.flags => .@;
                        (config.flagConfigs where .@value.hasDefault && .@ !<: existingFlags) >> .default
                )
            ;

            (:parse)
    )
