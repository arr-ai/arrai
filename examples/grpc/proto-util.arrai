let grpc = //{./grpc};

\wrap (
    field: \. $`${grpc.type(.)} ${//str.sub(.key, "-", "")} = ${.attrs.rpcId};`,

    imports: \fields
        fields where(//str.contains(grpc.type(.@item), "google.protobuf")) >>
            $`import "${//str.sub(grpc.type(.), ".", "/")}.proto";`,

    endpointInfo: \ep
        let method = //str.sub(//str.title(//str.lower(ep.name)), "-", "");
        let paramName = cond (
            wrap: method + "Request",
            *: $"${ep.params >> grpc.type(.)::, }",
        );
        let streamRes = cond (
            ep.attrs.stream: //str.sub(ep.ret("ok"), "sequence of", "stream"),
            *: //str.sub(ep.ret("ok"), "sequence of ", "") + "s",
        );
        let responseName = cond (wrap: method + "Response", *: streamRes);
        (
            method:       method,
            paramName:    paramName,
            responseName: responseName,
            rpcMethod:    $`rpc ${method}(${paramName}) returns (${responseName}) {}`,
        ),

    wrapSequence: \ep
        let type = //str.sub(ep.ret("ok"), "sequence of ", "");
        let wrapType = type + "s";
        let name = //str.lower(type) + "s";
        (
            grpcType: cond (!ep.attrs.stream: $`message ${wrapType} { repeated ${type} ${name} = 1; }`),
        ),
)
